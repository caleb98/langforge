plugins {
    id 'buildlogic.java-application-conventions'
    id 'org.openjfx.javafxplugin'
}

dependencies {
    implementation project(':langforge-core')
}

application {
    mainClass = 'net.calebscode.langforge.app.LangforgeApplicationLauncher'
}

javafx {
    version = '21.0.3'
    modules = [ 'javafx.controls' ]
}

def copyPluginJars = tasks.register('copyPluginJars', Copy) {
    group 'Distribution'
    description "Copies built jar files from each plugin project into this project's build dir."

    pluginProjects.each {
        dependsOn project(it).tasks.named('jar')
        from project(it).tasks.named('jar')
    }

    into layout.buildDirectory.dir('plugins')
}

tasks.named('startScripts') {
    applicationName = rootProject.name
    executableDir = ''

    unixStartScriptGenerator.template = resources.text.fromFile('start-scripts/unixStartScript.sh')
    windowsStartScriptGenerator.template = resources.text.fromFile('start-scripts/windowsStartScript.bat')
}

distributions {
    main {
        contents {
            from startScripts
            from(copyPluginJars) {
                into 'plugins'
            }

            // Filter out the start scripts that normally are placed in the bin
            // directory, since we want these at root level.
            eachFile { file ->
                if (file.path.startsWith('bin/') || file.path.startsWith('langforge-app/bin')) {
                    file.exclude()
                }
            }
        }
    }
}